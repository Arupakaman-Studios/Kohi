plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    versionCodeWithGPlay = 8
    versionCodeProGPlay = 8
    versionCodeFdroid = 1
}

android {

    signingConfigs {

        debug {
            storeFile file('../kohi_debug_android.keystore')
        }

        releaseGPlay {
            def props = new Properties()

            def fileInputStream = new FileInputStream(file('../secure.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile = file(props['storeFilePathFree'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }

        releaseProGplay {
            def props = new Properties()

            def fileInputStream = new FileInputStream(file('../secure.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile = file(props['storeFilePathPro'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.arupakaman.kohi"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode versionCodeWithGPlay
        versionName "1.0.5"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //resValue "string", "app_name", "Kohi"
            manifestPlaceholders = [appLabel: "Kohi"]

            //signingConfig signingConfigs.releaseProGplay
            signingConfig signingConfigs.releaseGPlay
        }
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            //resValue "string", "app_ame", "Kohi Dev"
            manifestPlaceholders = [appLabel: "Kohi Dev"]

            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "variant"
    productFlavors {
        withGPlay {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "true"
        }
        proGPlay {
            applicationId "com.arupakaman.kohi.donate"
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "false"
            buildConfigField "boolean", "isAdsOn", "false"
        }
        noGPlay {
            dimension "variant"
            buildConfigField "boolean", "isFDroid", "true"
            buildConfigField "boolean", "isAdsOn", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    sourceSets {
        withGPlay {
            res {
                srcDirs 'src/withGPlay/res'
            }
            java {
                srcDirs 'src/withGPlay/java'
            }
        }
        noGPlay {
            res {
                srcDirs 'src/noGPlay/res'
            }
            java {
                srcDirs 'src/noGPlay/java'
            }
        }
        proGPlay {
            res {
                srcDirs 'src/proGPlay/res'
            }
            java {
                srcDirs 'src/proGPlay/java'
            }
        }
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "androidx.multidex:multidex:2.0.1"

    /**
     *   With Google Play Dependencies
     */
    withGPlayImplementation platform('com.google.firebase:firebase-bom:26.5.0')
    withGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    withGPlayImplementation "com.google.firebase:firebase-ads"
    withGPlayImplementation 'com.google.android.play:core:1.10.2'
    withGPlayImplementation "com.google.firebase:firebase-messaging"

    proGPlayImplementation platform('com.google.firebase:firebase-bom:26.5.0')
    proGPlayImplementation 'com.google.android.play:core:1.10.2'
    proGPlayImplementation 'com.google.firebase:firebase-crashlytics-ktx'
    proGPlayImplementation "com.google.firebase:firebase-messaging"
    proGPlayImplementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}